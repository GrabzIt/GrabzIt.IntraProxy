; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AD9E5127-933E-46F8-A35E-23E97E5B08B2}
AppName=GrabzIt IntraProxy
AppVersion=1.1.0
VersionInfoVersion=1.1.0
;AppVerName=GrabzIt IntraProxy 0.0.1
AppPublisher=GrabzIt Limited
AppPublisherURL=https://grabz.it/
AppSupportURL=https://grabz.it/
AppUpdatesURL=https://grabz.it/
OutputBaseFilename=GrabzItIntraProxy
UsePreviousAppDir=no
DisableDirPage=yes
DisableProgramGroupPage=yes
DefaultDirName={sd}\GrabzItIntraProxy
LicenseFile=C:\Apps\GrabzItIntraProxy\intraproxy\licence.txt
Compression=lzma
SolidCompression=yes
SetupIconFile=C:\Apps\GrabzItIntraProxy\installer.ico
UninstallDisplayIcon=C:\Apps\GrabzItIntraProxy\installer.ico
PrivilegesRequired=admin
SignTool=signtool
SignedUninstaller=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "C:\Apps\GrabzItIntraProxy\Output\intraproxy.jar"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Apps\GrabzItIntraProxy\intraproxy\config.properties"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Apps\GrabzItIntraProxy\nssm\nssm.exe"; DestDir: "{app}"; Flags: ignoreversion sign

[Icons]
Name: "{group}\{cm:UninstallProgram,GrabzIt IntraProxy}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\nssm.exe"; Parameters: " install GrabzItIntraProxy java -jar ""{app}\intraproxy.jar"""; Flags: runhidden runascurrentuser; Description: "Installing Service"; StatusMsg: "Installing Service..."; Check: IsJREInstalled
Filename: "{sys}\sc.exe"; Parameters: "start GrabzItIntraProxy"; Flags: runhidden runascurrentuser; Description: "Starting Service"; StatusMsg: "Starting Service..."
Filename: "http://localhost:10000/grabzit://dashboard.html"; Flags: shellexec runhidden runasoriginaluser waituntilterminated; Description: "Starting Dashboard"; StatusMsg: "Starting Dashboard..."; BeforeInstall: Sleep(5000)

[UninstallRun]
Filename: {sys}\sc.exe; Parameters: "stop GrabzItIntraProxy" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "delete GrabzItIntraProxy" ; Flags: runhidden

[Code]
#define MinJRE "1.6"
#define WebJRE "http://www.oracle.com/technetwork/java/javase/downloads/index.html"

function IsJREInstalled: Boolean;
var
  JREVersion: string;
begin
  // read JRE version
  Result := RegQueryStringValue(HKLM32, 'Software\JavaSoft\Java Runtime Environment',
    'CurrentVersion', JREVersion);
  // if the previous reading failed and we're on 64-bit Windows, try to read 
  // the JRE version from WOW node
  if not Result and IsWin64 then
    Result := RegQueryStringValue(HKLM64, 'Software\JavaSoft\Java Runtime Environment',
      'CurrentVersion', JREVersion);
  // if the JRE version was read, check if it's at least the minimum one
  if Result then
    Result := CompareStr(JREVersion, '{#MinJRE}') >= 0;
end;

function InitializeSetup: Boolean;
var
  ErrorCode: Integer;
begin
  Result := True;
  // check if JRE is installed; if not, then...
  if not IsJREInstalled then
  begin
    // show a message box and let user to choose if they want to download JRE;
    // if so, go to its download site and exit setup; continue otherwise
    if MsgBox('Java is required. Do you want to download it now ?',
      mbConfirmation, MB_YESNO) = IDYES then
    begin
      Result := False;
      ShellExec('', '{#WebJRE}', '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
    end;
  end;
end;
